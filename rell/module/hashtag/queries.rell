query get_topics_by_tag_prior_to_timestamp(tag: text, timestamp, page_size: integer) {
    return topic_tag@*{ 
        .name == tag,
        .topic.last_modified < timestamp
    } (
        id=.topic.id,
        author=.topic.author.name,
        title=.topic.title,
        message=.topic.message,
        timestamp=.topic.timestamp,
        -sort lastModified=.topic.last_modified
    ) limit page_size;
}

query get_topics_by_followed_tags_prior_to_timestamp(username: text, timestamp, page_size: integer) {
    val user = user@{ .name == username};
    return (topic_tag, tag_following) @* {
        tag_following.user == user,
        tag_following.tag == topic_tag.name,
        topic_tag.topic.last_modified < timestamp
    }
    (
        id=topic_tag.topic.id,
        author=topic_tag.topic.author.name,
        title=topic_tag.topic.title,
        message=topic_tag.topic.message,
        timestamp=topic_tag.topic.timestamp,
        -sort lastModified=topic_tag.topic.last_modified
    ) limit page_size;
}

query get_followed_topics(username: text) {
    val user = user@{ .name == username};
    return tag_following@*{ user } ( .tag );
}

query get_all_tags() {
    return topic_tag@*{} ( .name );
}

query get_tags_since(timestamp) {
    return topic_tag@*{ .timestamp > timestamp }( .name );
}
